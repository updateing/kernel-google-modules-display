# NOTE: THIS FILE IS EXPERIMENTAL FOR THE BAZEL MIGRATION AND NOT USED FOR
# YOUR BUILDS CURRENTLY.
#
# It is not yet the source of truth for your build. If you're looking to modify
# the build file, modify the Android.bp file instead. Do *not* modify this file
# unless you have coordinated with the team managing the Soong to Bazel
# migration.

load("//build/kernel/kleaf:kernel.bzl", "kernel_module")

filegroup(
    name = "headers",
    srcs = glob([
        "samsung/**/*.h",
    ]),
    visibility = [
        "//private/google-modules/touch:__subpackages__",
    ],
)

[kernel_module(
    name = "samsung.{}".format(kernel_build),
    srcs = glob([
        "samsung/**/*.c",
        "samsung/**/*.h",
        "Kbuild",
        "samsung/Kbuild",
        "samsung/panel/Kbuild",
    ]) + [
        "//private/google-modules/display/include:headers",
        "//private/google-modules/soc/gs:{}".format(soc_headers),
    ],
    outs = [
        "samsung/exynos-drm.ko",
        "samsung/panel/panel-samsung-drv.ko",
        "samsung/panel/panel-samsung-emul.ko",
        "samsung/panel/panel-samsung-s6e3fc3.ko",
        "samsung/panel/panel-samsung-s6e3fc3-p10.ko",
        "samsung/panel/panel-samsung-s6e3hc2.ko",
        "samsung/panel/panel-samsung-s6e3hc3.ko",
        "samsung/panel/panel-samsung-s6e3hc3-c10.ko",
        "samsung/panel/panel-samsung-s6e3hc4.ko",
        "samsung/panel/panel-samsung-sofef01.ko",
        "samsung/panel/panel-boe-nt37290.ko",
    ],
    kernel_build = "//private/google-modules/soc/gs:{}".format(kernel_build),
    kernel_module_deps = [
        "//private/google-modules/soc/gs:{}".format(soc),
    ],
    visibility = [
        "//private/google-modules/soc/gs:__pkg__",
    ],
) for kernel_build, soc, soc_headers in [
    ("slider", "gs101_soc", "gs101_soc_headers"),
    ("zuma_emulator", "zuma_soc_emulator", "soc_headers"),
    ("zuma_hybrid", "zuma_soc_hybrid", "soc_headers"),
]]
